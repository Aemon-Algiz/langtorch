plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'ai.knowly'
version '0.0.3'

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc).configureEach {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'LangTorch'
                description = 'Building composable LLM applications with Java.'
                url = 'https://github.com/Knowly-ai/langtorch'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/Knowly-ai/langtorch/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'langtorch-ops'
                        name = 'Langtorch Ops'
                        email = 'admin@knowly.ai'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Knowly-ai/langtorch.git'
                    developerConnection = 'scm:git:ssh://github.com/Knowly-ai/langtorch.git'
                    url = 'https://github.com/Knowly-ai/langtorch'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    // Lombok dependency and requires annotationProcessor
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    // Logging
    implementation 'com.google.flogger:flogger:0.7.4'
    implementation 'com.google.flogger:flogger-system-backend:0.7.4'
    // Serialization
    implementation 'com.google.code.gson:gson:2.9.1'
    // Regex expression matching
    implementation 'com.google.re2j:re2j:1.6'
    // Java Utils
    implementation "com.google.guava:guava:31.1-jre"
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.google.mug:mug:6.6'
    implementation 'com.google.auto.value:auto-value-annotations:1.10.1'
    annotationProcessor 'com.google.auto.value:auto-value:1.10.1'
    // third-party OpenAI java library
    implementation 'com.theokanning.openai-gpt3-java:service:0.11.1'
    // Injection framework
    implementation 'com.google.inject:guice:5.1.0'
    // Environment variables
    implementation 'io.github.cdimascio:java-dotenv:5.2.2'
}

// Testing related dependencies
dependencies {
    // Mocking
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    // Testing util
    implementation 'com.google.acai:acai:1.1'
    // Assertion
    implementation 'com.google.truth:truth:1.1.3'
}

test {
    useJUnitPlatform()
}
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
